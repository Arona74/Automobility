plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_version}-fabric" as Object

repositories {
    maven { url = 'https://maven.gegy.dev/' }
    maven { url = 'https://maven.terraformersmc.com/' }
    maven { url = 'https://aperlambda.github.io/maven' }
    // maven { url = 'https://hephaestus.dev/release' }
    maven { url = 'https://storage.googleapis.com/devan-maven/' }
    maven { url = 'https://api.modrinth.com/maven' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // Controller Support
    modImplementation "dev.lambdaurora:spruceui:${project.spruceui_version}"
    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
    modImplementation "maven.modrinth:midnightcontrols:${project.midnightcontrols_version}"
    api('org.aperlambda:lambdajcommon:1.8.1') {
        exclude group: 'com.google.code.gson'
        exclude group: 'com.google.guava'
    }

    // OBJ loader TODO: Update
    // modImplementation "dev.monarkhes:myron:${project.myron_version}"
    // include "dev.monarkhes:myron:${project.myron_version}"

    // Json't
    modImplementation "net.devtech:arrp:${project.arrp_version}"
    include "net.devtech:arrp:${project.arrp_version}"

    // Json entity models
    modImplementation "maven.modrinth:jsonem:${project.jsonem_version}"
    include "maven.modrinth:jsonem:${project.jsonem_version}"

    implementation project(':common')
}

processResources {
    from project(":common").sourceSets.main.resources

    inputs.property 'version', project.version
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = 'UTF-8'

    it.options.release = 17
}

loom {
    runs {
        client {
            setConfigName('(Fabric) Client')
            ideConfigGenerated(true)
            runDir('run')
            client()
        }
        server {
            setConfigName('(Fabric) Server')
            ideConfigGenerated(true)
            runDir('run')
            server()
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
    }
}
